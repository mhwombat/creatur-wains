------------------------------------------------------------------------
-- |
-- Module      :  ALife.Creatur.WainQC
-- Copyright   :  (c) Amy de BuitlÃ©ir 2013
-- License     :  BSD-style
-- Maintainer  :  amy@nualeargais.ie
-- Stability   :  experimental
-- Portability :  portable
--
-- QuickCheck tests.
--
------------------------------------------------------------------------
module ALife.Creatur.WainQC
  (
    test
  ) where

import ALife.Creatur.Genetics.BRGCWord8 (runDiploidReader, writeGene)
import ALife.Creatur.Wain.Agent
import qualified ALife.Creatur.Wain.BrainQC as B
import ALife.Creatur.Wain.TestUtils (prop_serialize_round_trippable,
  prop_genetic_round_trippable, prop_diploid_identity)
import Test.Framework (Test, testGroup)
import Test.Framework.Providers.QuickCheck2 (testProperty)
import Test.QuickCheck

instance Arbitrary Sex where
  arbitrary = elements [Male, Female]

equiv :: Wain -> Wain -> Bool
equiv a1 a2 =
  rAppearance a1 == rAppearance a2
  && rBrain a1 `B.equiv` rBrain a2
  && rSex a1 == rSex a2

strawMan :: Gen Wain
strawMan = do
    app <- arbitrary
    brain <- arbitrary
    sex <- arbitrary
    cond <- arbitrary
    return $ Wain "" app brain sex cond ([],[])
  
-- | Can't just generate an arbitrary genome and build an agent from
--   it, because random genomes tend to be invalid.
instance Arbitrary Wain where
  arbitrary = do
    name <- arbitrary
    a1 <- strawMan
    a2 <- strawMan
    let g1 = writeGene a1
    let g2 = writeGene a2
    let r = runDiploidReader (buildAgent False name) (g1, g2)
    case r of
      (Left s)   -> error . show $ s
      (Right r') -> return r'

test :: Test
test = testGroup "ALife.Wain.AgentQC"
  [
    testProperty "prop_serialize_round_trippable - Wain"
      (prop_serialize_round_trippable :: Wain -> Property),
    testProperty "prop_genetic_round_trippable - Wain"
      (prop_genetic_round_trippable (equiv) :: Wain -> Property),
    testProperty "prop_diploid_identity - Wain"
      (prop_diploid_identity (equiv) :: Wain -> Property)
  ]
